plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

version '1.0.1'
group 'com.berbix'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.eclipse.jetty:jetty-client:9.4.38.v20210224'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        berbixJava(MavenPublication) {
            from components.java

            pom {
                name = 'Berbix Java SDK'
                description = 'This Berbix Java library provides simple interfaces to interact with the Berbix API.'
                url = 'https://www.github.com/berbix/berbix-java'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id.set("nwadams")
                        name.set("Nick Adams")
                        email.set("nick@berbix.com")
                    }
                }
                scm {
                    connection.set("scm:git:https://github.com/berbix/berbix-java.git")
                    developerConnection.set("scm:git:https://github.com/berbix/berbix-java.git")
                    url.set("https://github.com/berbix/berbix-java")
                }
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = "file://${buildDir}/repo"
        }

        maven {
            name = "MavenCentral"
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = "$usr"
                password = "$pwd"
            }
        }
    }
}

signing {
    sign publishing.publications.berbixJava
}